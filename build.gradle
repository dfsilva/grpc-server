group 'br.com.diegosilva'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}


def akkaVersion = '2.5.4'
def grpcVersion = '1.6.1'

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:0.1.16"

    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    //akka
    compile "com.typesafe.akka:akka-actor_2.12:${akkaVersion}"
    compile "com.typesafe.akka:akka-remote_2.12:${akkaVersion}"
    compile "com.typesafe.akka:akka-cluster_2.12:${akkaVersion}"
    compile "com.typesafe.akka:akka-cluster-metrics_2.12:${akkaVersion}"
    compile "com.typesafe.akka:akka-cluster-tools_2.12:${akkaVersion}"

    compile 'io.reactivex.rxjava2:rxjava:2.1.2'

    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'

    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.3.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // To generate deprecated interfaces and static bindService method,
                // turn the enable_deprecated option to true below:
                option 'enable_deprecated=false'
            }
        }
    }
}

apply plugin: 'idea'
idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
    }
}

apply plugin: 'application'

startScripts.enabled = false

task gameServer(type: CreateStartScripts) {
    mainClassName = 'br.com.diegosilva.grpc.hello.GameServer'
    applicationName = 'grpc-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(gameServer)
    fileMode = 0755
}


